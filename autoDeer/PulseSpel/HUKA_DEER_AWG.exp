; 5p DEER Setup and Run 
; This is designed for C243 Bruker AWG Hybrid spectrometer ONLY. 
; DO NOT USE ON ANY OTHER spectrometer
;
;  G.Jechke, A.Doll, F.Breitgoff, H.Karas
;  2022


begin defs
 dim s[512]              ;     dimension of data trace [sx] for pi/2 tp=p1 +<x>/-<x>
 dim1 s[512]             ;     dimension [sx] for pi tp=p1 -<y> echo up!
 dim2 s[64]             ;     nutation on -<y> channel
 dim3 s[300]            ;     dimension [sx] for Field Sweep +<x>/-<x> pg=200ns, turn On-Board phase cycling off
 dim4 s[128]             ;      dimension [sx] for AWG seq inv (p2)
 dim5 s[193]             ;     dimension [sx] for standing DEER 
 dim6 s[238];               ;     dimension for DEER ; sx = (d2+d1-1*d3)/d30
 dim7 s[450]              ;     dimension [sx] for standing 5'-DEER 
 dim8 s[432]              ;     dimension for alternating 5'-DEER with two t0; sx =(d2-1*d3+d24)/d30
 dim9 s[1000]               ;    dimension for 4-DEER relaxation
 dim10 s[150]               ;    dimension for 5'-DEER relaxation
 dim11 s[512];
end defs

begin lists "+<x> (pi/2)"
 ph1 +<x>
 ph2 +<x>
 ph3 +<x>
 asg1 +a
 bsg1 +b
end lists 


begin lists1 "-<x> (pi/2)"
 ph1 -<x>
 ph2 +<x>
 ph3 +<x>
 asg1 +a
 bsg1 +b
end lists1

begin lists2 "-<y> echo up!"
 ph1 +<x> -<x>   
 ph2 -<y>
 ph3 +<x>
 asg1 +a -a                   
 bsg1 +b -b 
end lists2

begin lists3 "Field sweep +<x>/-<x>"
 ph1 +<x> -<x>  
 ph2 +<x>
 ph3 +<x>
 asg1 +a -a                   
 bsg1 +b -b 
end lists3        

begin lists4 "AWG +-<x> obs"
 ph1 ELDOR
 ph2 +<x> -<x>
 ph3 +<x>
 asg1 +a -a                  
 bsg1 +b -b
end lists4 

begin lists5 "DEER run AWG -+<x>"
 ph1 +<x> -<x>   
 ph2 -<y>
 ph3 ELDOR                 
 asg1 +a -a                  
 bsg1 +b -b  
end lists5

begin lists6 "DEER run AWG only +<x> for setup"
 ph1 +<x>   
 ph2 -<y>
 ph3 ELDOR                 
 asg1 +a                  
 bsg1 +b  
end lists6

begin lists7 "-<y> nutation"
 ph1 -<y> 
 ph2 +<x>  -<x>
 ph3 +<x>
 asg1 +a    -a             
 bsg1 +b    -b
end lists7

begin lists8 "ELDOR nutation"
 ph1 ELDOR
 ph2 +<x>  -<x>
 ph3 +<x>
 asg1 +a    -a
 bsg1 +b    -b
end lists8

; Standing 2-pulse echo for setup of observer pi/2 pulse 
;
begin exp "pi/2 tp=p1 +<x>/-<x>" [TRANS QUAD]         ; QUAD detection

p11=2*p1
p10=p1
   
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p10 [ph1]                ; pi/2 pulse at obs frequency + or -<x>
      d1                       ; tau                       
      p11 [ph2]                ; pi pulse at obs frequency +<x>
      d1                       ; tau
      d0                       ; constant acquisition delay
      dig [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   
end exp   
;

begin exp1 "pi tp=p1 -<y>" [TRANS QUAD]         ; QUAD detection

p11=p1
p10=p1
   
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p10 [ph1]                ; pi/2 pulse at obs frequency + or -<x>
      d1                       ; tau                       
      p11 [ph2]                ; pi pulse at obs frequency +<x>
      d1                       ; tau
      d0                       ; constant acquisition delay
      dig [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   
end exp1   
;

begin exp2 "-<y> nutation" [INTG QUAD]         ; QUAD detection\n\

p10 = p1                    ; Pulse Length
pg = 2 * p10                ; acquisition time
d30 = d29 * sx              ; Nutation Step Size
d30 = d30 + 1000            ; Pulse Delay
d25 = 200 - pg              ; trigger correction for shorter gate\n\
d25 = d25 / 2
for k=1 to n
  dx=0
  p20=0
  totscans(n)
  sweep x=1 to sx
    shot i=1 to h              ; accumulation loop\n\
      d9                       ; DAF\n\
      p20 [ph1]
      d30
      p1 [ph2]                 ; 1st pulse and phase program\n\
      d1                       ; tau                       \n\
      p10 [ph3]                 ; 2nd pulse and phase program\n\
      d1                       ; tau\n\
      d0                       ; constant acquisition delay\n\
      d25                      ; trigger correction shorter gate\n\
      acq [sg1]                ; acquisition\n\
    next i                     ; end of accumulation loop\n\
    dx=dx+d29
    p20=p20+d29
   next x
scansdone(k)
next k
end exp2

begin exp3 "Field Sweep +<x>/-<x> pg=200ns" [INTG QUAD]        ; QUAD detection

pg=200                         ; integration gate
p11=2*p1

 for k=1 to n                  ; averaging loop

  totscans(n)                  ; output of total number of scans

  bsweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 12 ns pulse at + or -<x>
      d1                       ; tau                       
      p11 [ph2]                 ; 24 ns pulse at +<x>
      d1                       ; tau
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   next x                      ; end of sweep loop

  scansdone(k)                 ; output of scans done

 next k

end exp3 


begin exp4 "AWG seq inv (p2)" [INTG QUAD]         ; QUAD detection

p11 = 2 * p1
; p10=p0/2
p12 = p2 + 8
p13 = 16
d10 = d0
pg = 200
dx = 0

d13 = d7;

pg=2*p1;                        ; integration gate, standard 32 ns
 d25=200-pg                    ; trigger correction for shorter gate
 ;d25=d25-p0
 d25=d25/2
d25 = d25 + 6

for k=1 to n;
dx = 0                          ;Axis to zero 
totscans(n)
sweep x=1 to sx
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p12 [ph1]                ; AWG pulse at ELDOR channel 
      d13
      p1 [ph2]                 ; pulse at +<x> or -<x>
      d1                       ; tau                       
      p11 [ph3]                ; pulse at +<x>
      d1                       ; tau
      d10                      ; constant acquisition delay
      d25                      ; integration gate correction
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
dx = dx + 1
next x
scansdone(k)
next k
   
end exp4

begin exp5 "Standing 4-DEER AWG" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 
 for k=1 to n                  ; averaging loop

 pg = p1;                      ; integration gate, standard 32 ns
 d25 = 200 - pg                ; trigger correction for shorter gate
 d25 = d25 - p1
 d25 = d25 / 2
 d4 = d1 + d2                  ; second interpulse delay of observer
                               ; sequence, available observer window
 d4 = d4 - d3                  ; subtract deadtime
 d13 = d3                      ; initial delay between second observer
                               ; pulse and pump pulse
 d21 = d1                      ; initial tau1 value

 p12 = p2 ;+8
p10 = p1; to get all the same lengths

   for j=1 to m                ; tau1 averaging loop for suppression of
;                                nuclear modulations
      d23 = d13                  ; reset initial pump pulse delay
      d24 = d4                   ; reset initial delay of last observer
;                                pulse
      sweep x=1 to sx          ; sweep loop
       shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p1 [ph1]             ; 12 ns pulse at +<x> or -<x>
         d21                   ; tau1
         p1 [ph2]              ; 12 ns pulse at -<y>
         d23                   ; pump pulse delay, initially deadtime
         p12 [ph3]              ; AWG pulse (pi)
         d24                   ; tau1+tau2-d13
         p1 [ph2]             ; 12 ns pulse at +<y>
         d2                    ; tau2
         d0                    ; constant acquisition delay
         d25                   ; trigger correction
         acq [sg1]             ; acquisition
       next i                  ; end of accumulation loop
       dx = dx + d30               ; define time axis increment
      next x                   ; end of sweep loop  
      d21 = d21 + d31              ; increment tau1 for nucl. mod. averaging   
      d13 = d13 + d31              ; pump pulse delay must be incremented, too
   next j                      ; end of nucl. mod. averaging loop
   scansdone(k)                ; output of scans done  
 next k                        ; end of scan accumulation loop 
end exp5

begin exp6 "4-DEER AWG" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

 pg = 2 * p1;                  ; integration gate, standard 32 ns
 d25 = 200 - pg                ; trigger correction for shorter gate
 d25 = d25 - p1
 d25 = d25 / 2
 d4 = d1 + d2                  ; second interpulse delay of observer
;                                sequence, available observer window
 d4 = d4 - d3                  ; subtract deadtime
 d13 = d3                      ; initial delay between second observer
                               ; pulse and pump pulse
 d21 = d1                      ; initial tau1 value

 p12 = p2                      ; AWG Pulse Length
p10 = p1; to get all the same lengths

   for j=1 to m                ; tau1 averaging loop for suppression of
;                                nuclear modulations
      d23=d13                  ; reset initial pump pulse delay
      d24=d4                   ; reset initial delay of last observer
;                                pulse
      sweep x=1 to sx          ; sweep loop
       shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p1 [ph1]              ; 12 ns pulse at +<x> or -<x>
         d21                   ; tau1
         p1 [ph2]              ; 12 ns pulse at +<y>
         d23                   ; pump pulse delay, initially deadtime
         p12 [ph3]             ; AWG pulse (pi) at ELDOR channel
         d24                   ; tau1+tau2-d13
         p1 [ph2]              ; 12 ns pulse at +<y>
         d2                    ; tau2
         d0                    ; constant acquisition delay
         d25                   ; trigger correction
         acq [sg1]             ; acquisition
       next i                  ; end of accumulation loop
      
       dx  = dx + d30          ; define time axis increment
       d23 = d23 + d30         ; increment pump pulse delay
       d24 = d24 - d30         ; decrement delay after pump pulse   
      next x                   ; end of sweep loop  
      
      d21 = d21 + d31          ; increment tau1 for nucl. mod. averaging   
      d13 = d13 + d31          ; pump pulse delay must be incremented, too
   next j                      ; end of nucl. mod. averaging loop
   
   scansdone(k)                ; output of scans done  
 next k                        ; end of scan accumulation loop 

end exp6

begin exp7 "standing 5-dash-DEER" [INTG QUAD]

pg = p1;                        ; integration gate, standard 24 ns
d25 = 200 - pg                    ; trigger correction for shorter gate, not in use, remember to change d0 by hand 
d25 = d25 / 2
d25 = d25 - p0
d25 = d25 - 4


 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop


 ; delay between pi/2 and standing pump pulse 
 d8 = d2                       ; tau/2 
 d8 = d8 - d11                 ; subtract d11 = t0
 
 ; delay before moving pump pulse 
 d13 = d2 * 2                      ; in running 5pDEER d3
 
 ; delay between last observer pi and acquisition
 d21 = d2                      ; delay tau/2
 

    dx = 0                          
        sweep x=1 to sx          ; sweep loop
         shot i=1 to h           ; accumulation loop
           d9                    ; DAF
           p0 [ph1]              ; pulse at +<x> or -<x>
           d8                    ; tau/2 - d11
           p2 [ph3]              ; standing pump pulse from AWG
           d12                   ; t0
           p1 [ph2]              ; -<y> pulse (pi) 
           d23                   ; initially d3 = deadtime
           p2 [ph3]              ; moving pump pulse from AWG
           d15                   ; initially d2 - d23
	       p1 [ph2]              ; pulse at -<y> (pi)
           d21                   ; Delay from pump pulse to acquistion 
           d0                    ; constant acquisition delay
           d25                   ; trigger correction
           acq [sg1]             ; acquisition
         next i                  ; end of accumulation loop
         dx = dx + d30           ; define time axis increment
        next x                   ; end of sweep loop  

 next k
end exp7

begin exp8 "5p DEER" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

 pg=p1;                        ; integration gate, standard 24 ns
 pg = pg * 2
 d25=200-pg                    ; trigger correction for shorter gate, not in use, remember to change d0 by hand
 d25=d25/2
 d25=d25-p0
 d25=d25-4

 ; delay between pi/2 and standing pump pulse
 d8 = d2                      ; tau
 d8 = d8 - d11                 ; subtract d11 = t0
 
 ; delay before moving pump pulse
 d13 = d3                        ; initially deadtime, gets incremented by d30
 
 ; delay after moving pump pulse
 d4 = 2* d2
 d4 = d4 - d3                      ; initially d2 (tau) - deadtime, gets decremented by d30
 d4 = d4 + d24                     ; add shift
 
 ; delay between last observer pi and acquisition
 d21 = d2                          ; delay tau/2
 d21 = d21 + d24                   ; add shift d24 once
 
 ; nucl mod increment for tau delays
 d29 = d31
 
 
    for j=1 to m                 ; nucear modulation averaging loop
      d16 = d4                   ; reset pump pulse dlay
      d26 = d13                  ; reset delay after pump pulse

        sweep x=1 to sx          ; sweep loop
         shot i=1 to h           ; accumulation loop
           d9                    ; DAF
           p0 [ph1]              ; pulse at +<x> or -<x>
           d8                    ; tau/2 - d11
           p2 [ph3]              ; standing pump pulse from AWG        
           d11                   ; t0
           p1 [ph2]              ; -<y> pulse (pi)
           d26                  ; initially d3 = deadtime
           p2 [ph3]              ; moving pump pulse from AWG        
           d16                   ; initially d2 - deadtime + shift
           p1 [ph2]              ; pulse at -<y>       
           d21                   ;
           d0                    ; constant acquisition delay
           acq [sg1]             ; acquisition
         next i                  ; end of accumulation loop
         dx  = dx  + d30         ; define time axis increment
         d26 = d26 + d30         ; increment pump pulse delay
         d16 = d16 - d30         ; decrement delay after pump pulse  
        next x                   ; end of sweep loop 

      d8 = d8 + d29                ; increment for nucl. mod. averaging  
      d15 = d15 + d31
      d21 = d21 + d29
  next j                      ; end of nucl. mod. averaging loop
  scansdone(k)                ; output of scans done 
 
 next k                        ; end of scan accumulation loop

end exp8 

; Four-pulse DEER
begin exp9 "4-DEER Relax" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

    pg=p1;                        ; integration gate, standard 32 ns
    d25=200-pg                    ; trigger correction for shorter gate
    d25=d25-p1
    d25=d25/2

    d13=d3                        ; initial delay between second observer
                               ; pulse and pump pulse
    d31 = d30                  ; 
    d23 = 2*d1                    ; as start value for dx
 
    p12 = p2

    d24=d2;                   ; before: d24 = d2
	dx = d23
      sweep x=1 to sx          ; sweep loop

       shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p1 [ph1]             ; 12 ns pulse at +<x> or -<x>
         d1                   ; tau1
         p1 [ph2]              ; 12 ns pulse at +<y>
         d1                   ; pump pulse delay
         p12 [ph3]              ; AWG pulse (pi) at ELDOR channel
         d24                   ; tau2
         p1 [ph2]             ; 12 ns pulse at +<y>
         d24                    ; tau2
         d0                    ; constant acquisition delay
         d25                   ; trigger correction
         acq [sg1]             ; acquisition
       next i                  ; end of accumulation loop
    
       dx=dx+d31               ; define time axis increment as twice the step in d2 (axis = echo time)
       d24=d24+d30             ; decrement delay after pump pulse   
      next x                   ; end of sweep loop  
    scansdone(k)                ; output of scans done  
 next k                        ; end of scan accumulation loop 
end exp9  

begin exp10 "5p DEER relax" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

 pg=p1;                        ; integration gate, standard 24 ns
 d25=200-pg                    ; trigger correction for shorter gate, not in use, remember to change d0 by hand
 d25=d25/2
 d25=d25-p0
 d25=d25-4

 ; delay between pi/2 and standing pump pulse
 d8 = d2                      ; tau
 d8 = d8 - d11                 ; subtract d11 = t0
 
 ; delay before moving pump pulse
 d13 = d3                        ; initially deadtime, gets incremented by d30
 
 ; delay after moving pump pulse
 d4 = 2* d2
 d4 = d4 - d3                      ; initially d2 (tau) - deadtime, gets decremented by d30
 
 ; delay between last observer pi and acquisition
 d21 = d2                          ; delay tau/2
  
 

    sweep x=1 to sx          ; sweep loop
        shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p0 [ph1]              ; pulse at +<x> or -<x>
         d8                    ; tau/2 - d11
         d11                   ; t0
         p1 [ph2]              ; -<y> pulse (pi)
         d13                  ; initially d3 = deadtime
         d4                   ; initially d2 - deadtime
         p1 [ph2]              ; pulse at -<y>       
         d21                   ;
         d0                    ; constant acquisition delay
         acq [sg1]             ; acquisition
        next i                  ; end of accumulation loop
        dx  = dx  + d30         ; define time axis increment
        d8 = d8 + d30           ; increment pump pulse delay
        d4 = d4 + d30           ; increment pre pump delay
	d4 = d4 + d30
        d21 = d21 + d30         ; increment delay after pump pulse  
    next x                   ; end of sweep loop 

  scansdone(k)                ; output of scans done 
 
 next k                        ; end of scan accumulation loop

end exp10 

begin exp11 "Refocused Echo" [TRANS QUAD]


 ; delay between pi/2 and standing pump pulse
 d8 = d1                      ; tau
 
 ; delay before moving pump pulse
 d13 = d1                        ; initially deadtime, gets incremented by d30
 d13 = d13 + d2 ;
 ; delay between last observer pi and acquisition
 d21 = d2                          ; delay tau/2

 ; nucl mod increment for tau delays
 d29 = d31
 d26=d13
 
  shot i=1 to h           ; accumulation loop
    d9                    ; DAF
    p0 [ph1]              ; pulse at +<x> or -<x>
    d8                    ; tau1
    p1 [ph2]              ; -<y> pulse (pi)
    d26                  ; initially d3 = deadtime
    p1 [ph2]              ; pulse at -<y>       
    d21                   ;
    d0                    ; constant acquisition delay
    dig [sg1]             ; acquisition
  next i                  ; end of accumulation loop

end exp11