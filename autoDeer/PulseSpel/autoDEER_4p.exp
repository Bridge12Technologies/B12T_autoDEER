;
; DEER setup and run 
; G. Jeschke, 2000-2009
;
; ### This assumes that +x, -x, +<x> are observer frequency channels while
;     -<y> is a pump frequency (second frequency channel)
;     The pulse sequence must be adjusted for the commercial Bruker ELDOR
;     setup!
;     pulse lengths, delays etc. are optimzed for X band
;     for best results, a 3 mm split-ring resonator is recommended
;  
begin defs
 dim s[512]               ;     dimension of data trace [sx] for setup
 dim1 s[512]              ;     dimension [sx] for setup
 dim2 s[512]              ;     dimension [sx] for set-up
 dim3 s[1024]             ;     dimension [sx] for field sweep
 dim4 s[231]              ;     dimension [sx] for DEER
;                               ### must be adapted to delays!
;                               sx =(d1+d2-2*d3)/d30
 dim5 s[193]              ;     dimension for standing DEER
 dim6 s[650,10]           ;     dimension [sx] for field-averaged DEER
 dim7 s[1024]             ;     dimension [sx] for remote echo decay
 dim8 s[30]              ;     dimension [sx] for ELDOR nutation
   
end defs
;
; Pulse lengths:
;
; p0    observer pi pulse (frq1), recommendation for nitroxides:        32 ns
; p1    observer pi/2 pulse (frq1), recommendation for nitroxides:      32 ns
; p2    pump pi pulse (frq2), recommendation for nitroxides:            12 ns
;       at a frequency difference of frq1-frq2=60... 70  MHz 
;
; Variable delays:
;
; d0    acquisition trigger,    observation of defence pulses:  0 ns
;                               pulse setup:                    440 ns
;                               field sweep:                    440 ns
;                               DEER runs:                      484 ns
;                               ### depends on video bandwidth!
;                               ### 50 MHz video bandwidth recommended
;                               ### may vary between spectrometers!
; d1    tau1,                   absolute minimum:               80 ns
;                               recommended minimum:            120 ns
;                               recommended standard value:     200 ns
;                               ### short T2 -> minimum d1
;                               ### long deadtime -> longer d1
; d2    tau2,                   typical values:                 1-6 ï¿½s
;                               ### short T2 -> short d2
;                               ### high concentration -> short d2
;                               ### long distances -> long d2
;                               recommendation: increase d2 until echo
;                               amplitude is half of maximum value in
;                               SpecJet window at full receiver gain (66 dB)
;                               for overnight measurements, at 60 dB
;                               receiver gain for measurement times of 1 h,
;                               at 54 dB for measurement times of 15 min
; d3    deadtime,               window at the beginning and end of the
;                               second observer interpulse delay where no
;                               data are acquired, set to 80 ns
;                               ### do not change without need
;
; Variable increments:
;
; d30                           increment for dipolar time evolution, typically 8 ns
; d31                           increment for nuclear modulation averaging,
;                               typically 8 ns
;
;
; Fixed times:
;
; pg    integration gate,       field sweeps:   200 ns
;                               DEER:           p0 (pi pulse length)
; d9    DAF,                    (delay after flash), delay of experiment with
;                               respect to external trigger
;
; Other variables used (definition file settings are superseeded)
;
; p10, p11, p12, d4, d10, d13, d21, d23, d24
; d25, d26 for sensDEER
;
;
;
begin lists "+<y> (pi/2)"
 ph1 +<y>
 ph2 +<y>
 ph3 +<y>
 asg1 +a
 bsg1 +b
end lists 


begin lists1 "-<y> (pi/2)"
 ph1 -<y>   
 ph2 +<y>
 ph3 +<y>                  
 asg1 +a                   
 bsg1 +b  
end lists1

begin lists2 "+<x> (pi frq1)"
 ph1 +<x>   
 ph2 +<x>
 ph3 +<y>                  
 asg1 +a                   
 bsg1 +b  
end lists2
 
begin lists3 "-<x> (pi frq1)"
 ph1 -<x>   
 ph2 -<x>
 ph3 +<y>                  
 asg1 +a                   
 bsg1 +b  
end lists3
 
begin lists4 "Field sweep +<x>/-<x> (pi frq1)"
 ph1 +<x> -<x>  
 ph2 +<x>
 ph3 +<y>                  
 asg1 +a -a                   
 bsg1 +b -b 
end lists4 

begin lists5 "ELDOR (pi frq2)"
 ph1 ELDOR  
 ph2 +<x>
 ph3 +<x>                  
 asg1 +a                  
 bsg1 +b 
end lists5
 
begin lists6 "DEER setup (only observer pulses)"
 ph1 +<y>   
 ph2 +<x>
 ph3 *                 
 asg1 +a                   
 bsg1 +b  
end lists6
 
begin lists7 "DEER run"
 ph1 +<y> -<y>   
 ph2 +<x>
 ph3 ELDOR                 
 asg1 +a -a                  
 bsg1 +b -b  
end lists7                  
 
begin lists8 "ELDOR nutation"
 ph1 ELDOR 
 ph2 +<y> -<y>
 ph3 +<y>        
 asg1 +a -a                  
 bsg1 +b -b
end lists8

begin lists9 "+<x> nutation"
 ph1 +<x> 
 ph2 +<y> -<y>
 ph3 +<x>        
 asg1 +a -a                  
 bsg1 +b -b
end lists9

begin lists10 "DEER run Q"
 ph1 +<x> -<x>   
 ph2 +<x>
 ph3 ELDOR                 
 asg1 +a -a                  
 bsg1 +b -b  
end lists10         

;
;
; Standing 2-pulse echo for setup of observer pi pulses
;

begin exp "pi tp=p0 +<x>/-<x>" [TRANS QUAD]         ; QUAD detection

p11=p0
p10=p0/2
   
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p10 [ph1]                ; 1st pulse and phase program
      d1                       ; tau                       
      p11 [ph2]                ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      dig [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   
end exp   

;
;
; Standing 2-pulse echo for setup of observer pi/2 pulse 
;

begin exp1 "pi/2 tp=p1 +<y>/-<y>" [TRANS QUAD]         ; QUAD detection

p11=2*p1
p10=p1
   
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p10 [ph1]                ; 1st pulse and phase program
      d1                       ; tau                       
      p11 [ph2]                ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      dig [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   
end exp1   

;
;
; Standing inversion recovery for setup of pump pi pulse 


begin exp2 "pi tp=p2 ELDOR frq2" [TRANS QUAD]         ; QUAD detection

p11=p0
p10=p0/2
p12=p2
d13=400
d10=d0
dx=0
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p12 [ph1]
      d13
      p10 [ph2]                ; 1st pulse and phase program
      d1                       ; tau                       
      p11 [ph3]                ; 2nd pulse and phase program
      d1                       ; tau
      d10                      ; constant acquisition delay
      dig [sg1]                ; acquisition
    next i                     ; end of accumulation loop
   
end exp2   


; 2-Pulse Field Sweep


begin exp3 "Field Sweep +<x>/-<x> pg=200ns" [INTG QUAD]        ; QUAD detection

pg=200                         ; integration gate
p10=p1*2

 for k=1 to n                  ; averaging loop

  totscans(n)                  ; output of total number of scans

  bsweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 1st pulse and phase program
      d1                       ; tau                       
      p10 [ph2]                ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   next x                      ; end of sweep loop

  scansdone(k)                 ; output of scans done

 next k

end exp3 


; Four-pulse DEER


begin exp4 "DEER" [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

 ;pg=p0;                        ; integration gate, standard 32 ns
 
d25=200-pg                    ; trigger correction for shorter gate
 d25=d25-p0
 d25=d25/2
 d4=d1+d2                      ; second interpulse delay of observer
;                                sequence, available observer window
 d4=d4-d3                      ; subtract deadtime
 d13=d3                        ; initial delay between second observer
                               ; pulse and pump pulse
 d21=d1                        ; initial tau1 value

   for j=1 to m                ; tau1 averaging loop for suppression of
;                                nuclear modulations
      d23=d13                  ; reset initial pump pulse delay
      d24=d4                   ; reset initial delay of last observer
;                                pulse
      sweep x=1 to sx          ; sweep loop
       shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p1 [ph1]              ; 1st observer pulse (pi/2) and phase program
         d21                   ; tau1
         p0 [ph2]              ; 2nd observer pulse (pi) and phase program
         d23                   ; pump pulse delay, initially deadtime
         p2 [ph3]              ; pump pulse (pi)
         d24                   ; tau1+tau2-d13
         p0 [ph2]             ; 3rd observer pulse (pi) and phase program
         d2                    ; tau2
         d0                    ; constant acquisition delay
         d25                   ; trigger correction
         acq [sg1]             ; acquisition
       next i                  ; end of accumulation loop
       dx=dx+d30               ; define time axis increment
       d23=d23+d30             ; increment pump pulse delay
       d24=d24-d30             ; decrement delay after pump pulse   
      next x                   ; end of sweep loop  
      d21=d21+d31              ; increment tau1 for nucl. mod. averaging   
      d13=d13+d31              ; pump pulse delay must be incremented, too
   next j                      ; end of nucl. mod. averaging loop
   scansdone(k)                ; output of scans done  
 next k                        ; end of scan accumulation loop 
end exp4  


begin exp5 "standing DEER" [INTG QUAD]

 for k=1 to n                  ; averaging loop

 pg=p0;                        ; integration gate, standard 32 ns
 d13=d1
 d4=d2
 d25=200-pg                    ; trigger correction for shorter gate
 d25=d25-p0
 d25=d25/2
 totscans(n)                   ; output of total number of scans
 
   sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 1st pulse and phase program
      d1                       ; tau1
      p0 [ph2]                 ; 2nd pulse and phase program
      d13                       
      p2 [ph3]                 ; pi pulse on second frequency
      d4                       ; tau1+tau2-d13
      p0 [ph2]
      d2
      d0                       ; constant acquisition delay
      d25                      ; trigger correction shorter gate
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; define trigger increment   
   next x                      ; end of sweep loop
   dx=0
  
  scansdone(k)                 ; output of scans done
  
 next k
  
end exp5  

begin exp6 "DEER field avg." [INTG QUAD]

 totscans(n)                   ; output of total number of scans
 for k=1 to n                  ; averaging loop

; pg=p0;                        ; integration gate, standard 32 ns
 bsweep y=1 to sy
 d4=d1+d2                      ; second interpulse delay of observer
;                                sequence, available observer window
 d4=d4-d3                      ; subtract deadtime
 d13=d3                        ; initial delay between second observer
                               ; pulse and pump pulse
 d21=d1                        ; initial tau1 value
 d25=200-pg                    ; trigger correction for shorter gate
 d25=d25-p0
 d25=d25/2

   for j=1 to m                ; tau1 averaging loop for suppression of
;                                nuclear modulations
      d23=d13                  ; reset initial pump pulse delay
      d24=d4                   ; reset initial delay of last observer
;                                pulse
      sweep x=1 to sx          ; sweep loop
       shot i=1 to h           ; accumulation loop
         d9                    ; DAF
         p1 [ph1]              ; 1st observer pulse (pi/2) and phase program
         d21                   ; tau1
         p0 [ph2]              ; 2nd observer pulse (pi) and phase program
         d23                   ; pump pulse delay, initially deadtime
         p2 [ph3]              ; pump pulse (pi)
         d24                   ; tau1+tau2-d13
         p0 [ph2]             ; 3rd observer pulse (pi) and phase program
         d2                    ; tau2
         d0                    ; constant acquisition delay
         d25                   ; trigger correction shorter gate
         acq [sg1]             ; acquisition
       next i                  ; end of accumulation loop
       dx=dx+d30               ; define time axis increment
       d23=d23+d30             ; increment pump pulse delay
       d24=d24-d30             ; decrement delay after pump pulse   
      next x                   ; end of sweep loop  
      d21=d21+d31              ; increment tau1 for nucl. mod. averaging   
      d13=d13+d31              ; pump pulse delay must be incremented, too
   next j                      ; end of nucl. mod. averaging loop
   next y
   scansdone(k)                ; output of scans done  
 next k                        ; end of scan accumulation loop 
end exp6  

begin exp7 "Remote echo decay" [INTG QUAD]

 for k=1 to n                  ; averaging loop

 pg=p0;                        ; integration gate, standard 32 ns
 d4=2*d1
 d25=200-pg                    ; trigger correction for shorter gate
 d25=d25/2

 totscans(n)                   ; output of total number of scans
 
   sweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 1st pulse and phase program
      d1                       ; tau1
      p0 [ph2]                 ; 2nd pulse and phase program
      d1
      d4
      p0 [ph2]
      d4
      d0                       ; constant acquisition delay
      d25                      ; trigger correction shorter gate
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+d30                  ; define trigger increment
    d4=d4+d30   
   next x                      ; end of sweep loop
   dx=0
  
  scansdone(k)                 ; output of scans done
  
 next k
  
end exp7  

;
;
; ELDOR nutation for testing inversion efficiency and pump field 


begin exp8 "ELDOR nutation" [INTG QUAD]         ; QUAD detection

pg=p0
p3=p1*2
d30=4*sx
d30=d30+200
d25=200-pg                    ; trigger correction for shorter gate
d25=d25/2
for k=1 to n
  dx=0
  p20=0
  totscans(n)
  sweep x=1 to sx
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p20 [ph1]
      d30
      p1 [ph2]                 ; 1st pulse and phase program
      d1                       ; tau                       
      p3 [ph3]                 ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      d25                      ; trigger correction shorter gate
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop
    dx=dx+2
    p20=p20+2
   next x
scansdone(k)
next k
end exp8   

