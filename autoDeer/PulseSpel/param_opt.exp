; This is the pulse spel script for parameter optimization for 4p/5p DEER
; Included here are experiments and phase cycling
;
; Hugo Karas 2021/2022

begin defs

dim s[150]          ; Carr Purcell exp

dim1 s[150]         ; tau 2 scan

dim2 s[64,64]       ; 2D Dec. 64

dim3 s[128,128]     ; 2D Dec. 128

dim4 s[256]         ; Constant tau1+tau2
                    ; This will need changing for these experiments as the delay can't become negative
                    ; sx = (d11-d13)/d12

dim5 s[300]         ; EDFS

dim6 s[512]         ; Hahn Echo

dim7 s[128]         ; Eldor Nutation


end defs

begin lists "16 pulse"
ph1 +x -x
ph2 +x +x +y +y -x -x -y -y
ph3 +x +x +x +x +x +x +x +x -x -x -x -x -x -x -x -x 
asg1 +a -a -a +a +a -a -a +a +a -a -a +a +a -a -a +a
bsg1 +b -b -b +b +b -b -b +b +b -b -b +b +b -b -b +b
end lists

begin lists1 "EDFS"
ph1 +x -x
ph2 +x
asg1 +a -a
bsg1 +b -b
end lists1

begin lists2 "Hahn"
ph1 +x -x
ph2 +x
asg1 +a -a
bsg1 +b -b
end lists2

begin lists3 "16 MPFU"
ph1 +<x> -<x>
ph2 +<x> +<x> +<y> +<y> -<x> -<x> -<y> -<y>
ph3 +<x> +<x> +<x> +<x> +<x> +<x> +<x> +<x> -<x> -<x> -<x> -<x> -<x> -<x> -<x> -<x> 
asg1 +a -a -a +a +a -a -a +a +a -a -a +a +a -a -a +a
bsg1 +b -b -b +b +b -b -b +b +b -b -b +b +b -b -b +b
end lists3

begin lists4 "ELDOR nut"
ph1 ELDOR ELDOR
ph2 +<x> -<x>
ph3 +<x> +<x>
asg1 +a -a
bsg1 +b -b
end lists4


begin exp "Carr Purcell exp" [INTG QUAD]

dx = d1
dy = d2

totscans(n)

pg = p0

for k=1 to n
d11 = d1
d13 = d2

for j=1 to m
    d21 = d11
    d22 = d13
    sweep x=1 to w

        shot i=1 to h
            d9
            p1[ph1]
            d21
            p0[ph2]
            d21
            d22
            p0[ph3]
            d22
            d0
            acq[sg1]
        next i
        dx = dx + d12
        d21 = d21 + d12
        d22 = d22 + d12
    next x


    w  = w +1
    sweep x=w to sx 

        shot i=1 to h
            d9
            p1[ph1]
            d21
            p0[ph2]
            d21
            d22
            p0[ph3]
            d22
            d0
            acq[sg1]
        next i
        dx = dx + d12
        d21 = d21 + d12
        d22 = d22 + d12
    next x

next j
scansdone(k)
next k

end exp

begin exp1 "tau 2 scan" [INTG QUAD]

dx = d1
dy = d2

totscans(n)

pg = p0

for k=1 to n
d11 = d1
d13 = d2

sweep x=1 to w

shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d14
d13 = d13 + d14
next x

w = w + 1
sweep x=w to sx
shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d14
d13 = d13 + d14
next x

scansdone(k)
next k

end exp1

begin exp2 "2D Dec. 64" [INTG QUAD]

dx = d1
dy = d2

c= n * sy
totscans(c)

pg = p0

for k=1 to n
d13 = d2

for y=1 to sy
d11 = d1
b=w
sweep x=1 to b

shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d12
d11 = d11 + d12
next x

b = b + 1
sweep x = b to sx

shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d12
d11 = d11 + d12
next x

dy = dy + d14
d13 = d13 + d14
m = k -1
m = m * sy
m = m + y
scansdone(m)
next y

next k
end exp2

begin exp3 "2D Dec. 128" [INTG QUAD]

dx = d1
dy = d2

c = n * sy
totscans(c)

pg = p0

for k=1 to n
d13 = d2

for y=1 to sy
d11 = d1
sweep x=1 to sx

shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d12
d11 = d11 + d12
next x

dy = dy + d14
d13 = d13 + d14
m = k*sy
m = m +y
scansdone(m)
next y

next k
end exp3

begin exp4 "Constant tau1+tau2" [INTG QUAD]

dx = d1 - d2

totscans(n)

pg = p0

for k=1 to n
d11 = d1
d13 = d2

sweep x=1 to sx

shot i=1 to h
d9
p1[ph1]
d11
p0[ph2]
d11
d13
p0[ph3]
d13
d0
acq[sg1]
next i
dx = dx + d12
d11 = d11 + d12
d13 = d13 - d12
next x

scansdone(k)
next k

end exp4

begin exp5 "EDFS"   [INTG QUAD]
pg=p2                         ; integration gate

 for k=1 to n                  ; averaging loop

  totscans(n)                  ; output of total number of scans

  bsweep x=1 to sx             ; sweep loop
    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 1st pulse and phase program
      d1                       ; tau                       
      p2 [ph2]                ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      acq [sg1]                ; acquisition
    next i                     ; end of accumulation loop 
   next x                      ; end of sweep loop

  scansdone(k)                 ; output of scans done

 next k

 end exp5

 begin exp6 "Hahn Echo" [TRANS QUAD]

    shot i=1 to h              ; accumulation loop
      d9                       ; DAF
      p1 [ph1]                 ; 1st pulse and phase program
      d1                       ; tau                       
      p2 [ph2]                ; 2nd pulse and phase program
      d1                       ; tau
      d0                       ; constant acquisition delay
      dig [sg1]               ; acquisition
    next i                     ; end of accumulation loop 


 end exp6

begin exp7 "nutation" [INTG QUAD]         ; QUAD detection\n\

pg = 2 * p1                ; acquisition time
d30 = d29 * sx              ; Nutation Step Size
d30 = d30 + 1000            ; Pulse Delay
for k=1 to n
  dx=0
  p20 = 0
  totscans(n)
  sweep x=1 to sx
    shot i=1 to h              ; accumulation loop\n\
      d9                       ; DAF\n\
      p20 [ph1]
      d30
      p1 [ph2]                 ; 1st pulse and phase program\n\
      d1                       ; tau                       \n\
      p1 [ph3]                 ; 2nd pulse and phase program\n\
      d1                       ; tau\n\
      d0                       ; constant acquisition delay\n\
      acq [sg1]                ; acquisition\n\
    next i                     ; end of accumulation loop\n\
    dx=dx+d29
    p20 = p20 +d29
   next x
scansdone(k)
next k
end exp7
